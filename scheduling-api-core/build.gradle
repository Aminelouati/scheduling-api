apply plugin: 'spring-boot'
apply plugin: 'war'

bootRepackage {
    enabled = true
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
    }
}

springBoot {
    mainClass = 'org.ow2.proactive.scheduling.api.Application'
}

war {
    baseName = 'scheduling-api'
    version = version
}

// FIXME: investigate why transitive dependencies of dependencies defined
// with providedCompile are still included in the WAR file that is generated
dependencies {

    // Note: all providedCompile dependencies need to be changed to compile dependencies while running the
    // application alone without embedded in the scheduler project

    compile 'commons-fileupload:commons-fileupload:1.3.1'

    compile 'org.apache.logging.log4j:log4j-web:2.7'

    /*
     * Spring boot 1.3.X relies on hibernate 4 which is not compatible
     * with the version that is used with Scheduling project
     */
    compile('org.springframework.boot:spring-boot-starter-data-jpa:1.3.3.RELEASE') {
        exclude module: 'hibernate-entitymanager'
        exclude module: 'spring-boot-starter-validation'
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile 'org.springframework.boot:spring-boot-starter-log4j2:1.3.3.RELEASE'
    compile('org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE') {
        exclude module: 'hibernate-entitymanager'
        exclude module: 'spring-boot-starter-validation'
        exclude module: 'spring-boot-starter-tomcat'
    }

    // The scope of this dependency has to be changed to compile
    // in order to start the application from your IDE
    providedCompile 'org.springframework.boot:spring-boot-starter-tomcat:1.3.3.RELEASE'

    compile 'com.graphql-java:graphql-java:2.2.0'

    compile 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
    compile 'org.hibernate:hibernate-core:5.1.0.Final'

    runtime 'org.hsqldb:hsqldb:r5658-f2e27a7'

    // The scope of this dependency has to be changed to compile
    // in order to start the application from your IDE
    providedCompile("org.ow2.proactive:scheduler-server:$version") {
        exclude module: 'hsqldb'
    }

    testCompile 'com.google.code.gson:gson:2.6.2'
    testCompile 'com.jayway.restassured:rest-assured:2.9.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.3.RELEASE'
}
